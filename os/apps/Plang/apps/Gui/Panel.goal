Panel
- select * from appInfos, write to %appInfos%
- if %appInfos.count% > 1 then call SelectApp, else call ProcessApp appInfo=%appInfos.first%

SelectApp
- ask user "Which app are you working on?", 
    selection: %appInfos.name%, on callback call LoadAppInfo, 
    answer is required, 
    write to %appInfo%
- call goal ProcessApp

ProcessApp
- if %appInfos.count% == 0 then call CreateApp
- select * from designs where appInfoId=%appInfo.id% order by updated desc, write to %designs%
- set %text% = "Welcome to GUI editor"
- write out json {design:%designs%, text:%text%, designFile:%designs.designFile.first%}

LoadAppInfo
- select * from appInfos, write to %appInfos%

CreateApp
- write out "Welcome to the GUI builder. Lets answer few questions before we continue"
- get db table information "appInfos", write to %appInfosTable%
- select name, description from iconFrameworks, write to %iconFrameworks%
- select name, description from cssFrameworks, write to %cssFrameworks%   
- select name, description from fontFamilies, write to %fontFamilies%        
- call goal AskQuestion

AskQuestion
- read llm/appInfo/createQuestionAppInfoSystem.llm, load vars, write to %system%
- [llm] system: %system%
        user: "Give me next question(s)"
        model: "gpt-4.1"
        scheme: {columnNames:string[], question:string}
        write to %prepQuestions%
- ask user %prepQuestions.question%, write %answer%
- read llm/appInfo/formatAnswerSystem.llm, load vars, write %system%
- [llm] system: %system%
        user: %answer%
        model: "gpt-4.1"
        continue: true
        scheme: {questions:[{columnName:string, answer:string|null}], hasAnsweredAll:true|false}
        write to %result%
- append %result.questions% to list %answers%
- if %result.hasAnsweredAll% then SaveToDb, else AskQuestion

SaveToDb
- write out "Save to db: %answers%"
- read llm/appInfo/insertToDbSystem.llm, load vars, write to %system%
- [llm] system: %system%
    user: %answers%
    scheme: {sql:string}
- execute sql statement %sql%, allow list:"appInfos"
    on error call FixInsert
- select * from appInfos order by id desc, return 1 row, %appInfo%
- write out "Done"

FixInsert
- write out %!error%
    