SendDebug
- set default value %plang.debugPort% = 60877
- post http://localhost:%plang.debugPort%/ 
    data: { "line":"%!Event.GoalStep.LineNumber%", "AbsolutePath":"%!Event.Goal.AbsoluteGoalPath%" }
    timeout 30 min
    write to %debugResponse%
- if %debugResponse.continue%, call SendDebugInfo

SendDebugInfo
- post http://localhost:%plang.debugPort%/ 
    data: {
        "!MemoryStack":"%!MemoryStack%",
        "!Context": "%!context%"
        "AbsolutePath":"%!Event.Goal.AbsoluteGoalPath%", 
        "!Goal":"%!Event.Goal%", 
        "!Step":"%!Event.GoalStep%",
        "!Instruction":"%!Event.Instruction%",
        "!Event":"%!Event%",
        "!IsHttpRequest":"%!IsHttpRequest%",
        "!StepIndex": %!Event.GoalStep.Index%,
        "!Error":"%!error%",
        "!GoalVariables":"%!Event.Goal.GetVariables()%"
        }
    dont sign
    timeout 30 min
    on error 'No connection could', call WriteToConsole
    on error, call WriteOutError
    write to %debugResponse%
- if %debugResponse.csharpDebugger% then
    - log warning 'Starting C# debugger'
    - [plang] start csharp debugger
- if %debugResponse.prFileName% is not empty then
    - log warning 'Running from step %debugResponse.prFileName%'
    - [plang] run from step %debugResponse.prFileName%

WriteToConsole
- logger warning 'Could not connect to IDE for debugging'

WriteOutError
- logger error %!error%
