RenderUserIntent
- set %templateName% = %Settings.Templates.name.first%
- read %options.OutputFile%, write to %currentUiFile%, "No UI has been created" if not found
- get all file names for '.md' files in "template/%templateName%", write to %components%
- read llm/RenderUserIntentSystem.llm, load vars, write to %system%
- [llm] system: %system%
        assistant: "<currentUiFile>%currentUiFile%</currentUiFile>"
        user: %!step.Text%
        model:"o3"
        scheme: {reasoning:string, pseudoCode:string}
        write to %result%
- call goal GenerateHtml user=%!step.Text%
- return %html%, css=%jsAndCss%, javascript=%jsAndCss.javascript%

GenerateHtml
- read llm/GenerateHtmlSystem.llm, write to %system%
- [llm] system: %system%
        continue from prev: true
        user: %user%
        model: "gpt-4.1"
        max length: 32768
        scheme: {html:string, components:string[]}
        write %result%
- if %result.components.count% > 0 then 
    - foreach %result.components% call ReadComponentFile item=%file%
    - call GenerateHtml user=%componentDocs%
    - end goal
- [llm] system: "Generate any javascript and css include <script> and <style>"
        continue from prev: true
        max length: 32768
        scheme: {javascript:string, css:string}        
        model: "gpt-4.1"
        write to %jsAndCss%
- save "%jsAndCss.css%\n%html%\n%jsAndCss.javascript%" to %options.OutputFile%

ReadComponentFile
- read template/%templateName%/%file%, include system, "%file% not found" if empty
    write to %readme%
- append to list %componentDocs% = %readme%